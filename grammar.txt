Grammar for Pascal Interpreter
================================
program : PROGRAM variable SEMI block DOT
block : declarations compound_statement
declarations : (VAR (variable_declaration SEMI)+)? 
               (FUNCTION ID (LPAREN formal_parameter_list RPAREN)? COLON type_spec SEMI block SEMI)*
             | empty
formal_parameter_list : formal_parameters
                      | formal_parameters SEMI formal_parameter_list
formal_parameters : ID (COMMA ID)* COLON type_spec
variable_declaration : ID (COMMA ID)* COLON type_spec
type_spec : INTEGER | REAL
compound_statement : BEGIN statement_list END
statement_list : statement
               | statement SEMI statement_list
statement : compound_statement
          | assignment_statement
          | if_statement
          | while_statement
          | for_statement
          | empty
assignment_statement : variable ASSIGN expr
if_statement : IF boolean_expression THEN statement (ELSE statement)? END
while_statement : WHILE boolean_expression DO statement
for_statement : FOR ID ASSIGN expr TO expr DO statement
              | FOR ID ASSIGN expr DOWNTO expr DO statement
boolean_expression : boolean_term ((OR) boolean_term)*
boolean_term : boolean_factor ((AND) boolean_factor)*
boolean_factor : NOT boolean_factor
               | LPAREN boolean_expression RPAREN
               | comparison
comparison : expr ((EQUAL | NOT_EQUAL | LESS_THAN | GREATER_THAN | 
                   LESS_EQUAL | GREATER_EQUAL) expr)?
variable : ID
expr : term ((PLUS | MINUS) term)*
term : factor ((MUL | INTEGER_DIV | FLOAT_DIV) factor)*
factor : PLUS factor
       | MINUS factor
       | INTEGER_CONST
       | REAL_CONST
       | LPAREN expr RPAREN
       | function_call
       | variable
function_call : ID LPAREN (expr (COMMA expr)*)? RPAREN
empty :

Tokens
======
PROGRAM, VAR, FUNCTION, BEGIN, END, IF, THEN, ELSE
WHILE, DO, FOR, TO, DOWNTO
INTEGER, REAL
INTEGER_CONST, REAL_CONST
ID (identifier)
ASSIGN (:=)
PLUS (+), MINUS (-), MUL (*), INTEGER_DIV (DIV), FLOAT_DIV (/)
EQUAL (=), NOT_EQUAL (<>), LESS_THAN (<), GREATER_THAN (>), 
LESS_EQUAL (<=), GREATER_EQUAL (>=)
AND, OR, NOT
LPAREN ((), RPAREN ()), SEMI (;), DOT (.), COLON (:), COMMA (,)
EOF
